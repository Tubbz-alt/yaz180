Name     YAZ180_Memory ;
PartNo   00 ;
Date     1/2/2018 ;
Revision 05 ;
Designer Phillip Stevens ;
Company  Emotiv8 ;
Assembly None ;
Location Melbourne Australia ;
Device   G16V8MA ;

/* *************** INPUT PINS *********************/

PIN   1     =  !PROGRAM            ; /* Program pin, ACTIVE LOW */
PIN   2     =  !MREQ               ; /* Memory Request, ACTIVE LOW */
PIN  [3..9] =  [A19..13]           ; /* Most significant digits of address bus to define mapping  */
PIN  11     =  !IORQ               ; /* IO Request, ACTIVE LOW */

/* *************** OUTPUT PINS *********************/

PIN  19  =  !APU_CS                ; /* Chip Select for APU, ACTIVE LOW            */
PIN  18  =  !RAM_CS                ; /* Chip Select for SRAM, ACTIVE LOW           */ 
PIN  17  =  !FLASH_CS              ; /* Chip Select for Flash, ACTIVE LOW          */ 
PIN  16  =  !USB_CS                ; /* Chip Select for USB, ACTIVE LOW - Combine with !WR and !RD as needed. */
PIN  15  =  !I2C_CS2               ; /* Chip Select for I2C Interface #2, ACTIVE LOW */
PIN  14  =  !I2C_CS1               ; /* Chip Select for I2C Interface #1, ACTIVE LOW */
PIN  13  =  !DIO_CS                ; /* Chip Select or Digital IO, ACTIVE LOW */
PIN  12  =   BREAK                 ; /* Program Break Point, Initiate Single Step, ACTIVE HIGH */

/* *************** DECLARATIONS ********************/

FIELD MEM_ADDRESS = [A19..13]      ; /* Most significant bits of memory address bus */

FIELD IO_ADDRESS =  [A15..13]      ; /* Most significant bits of IO (memory) address bus */

/* *************** MEMORY LOGIC *********************/

CONDITION {
	IF (            MREQ & !IORQ & MEM_ADDRESS:[0C000..0FFFF] )      OUT RAM_CS ;  /* BANK00 12kB and COMMON1 4kB (COMMON1 base address is flexible) */
	IF ( !PROGRAM & MREQ & !IORQ & MEM_ADDRESS:[20000..2FFFF] )      OUT RAM_CS ;  /* BANK02 SRAM in Run State (not Programme State) */
	IF (            MREQ & !IORQ & MEM_ADDRESS:[30000..FFFFF] )      OUT RAM_CS ;  /* BANK03 through BANK15 SRAM */
/*	IF ( !PROGRAM & MREQ & !IORQ & MEM_ADDRESS:[40000..4FFFF] )      OUT RAM_CS ;  /* BANK04 SRAM in Run State (not Programme State) */
/*	IF (            MREQ & !IORQ & MEM_ADDRESS:[50000..FFFFF] )      OUT RAM_CS ;  /* BANK05 through BANK15 SRAM */
/*	IF ( !PROGRAM & MREQ & !IORQ & MEM_ADDRESS:[80000..8FFFF] )      OUT RAM_CS ;  /* BANK08 SRAM in Run State (not Programme State) */
/*	IF (            MREQ & !IORQ & MEM_ADDRESS:[90000..FFFFF] )      OUT RAM_CS ;  /* BANK09 through BANK15 SRAM */
	}

CONDITION {
	IF ( !PROGRAM & MREQ & !IORQ & MEM_ADDRESS:[00000..0BFFF] )      OUT FLASH_CS ; /* BANK00 YABIOS and system libraries */
	IF (            MREQ & !IORQ & MEM_ADDRESS:[10000..1FFFF] )      OUT FLASH_CS ; /* BANK01 (Snapshot storage) SST39SF010A 128kB*/
	IF (  PROGRAM & MREQ & !IORQ & MEM_ADDRESS:[20000..2FFFF] )      OUT FLASH_CS ; /* In Programming State, force BANK00 to appear as BANK02 - SST39SF010A 128kB */
/*	IF (            MREQ & !IORQ & MEM_ADDRESS:[10000..3FFFF] )      OUT FLASH_CS ; /* BANK01, BANK02, BANK03 (Snapshot storage) - SST39SF020A 256kB */
/*	IF (  PROGRAM & MREQ & !IORQ & MEM_ADDRESS:[40000..4FFFF] )      OUT FLASH_CS ; /* In Programming State, force BANK00 to appear as BANK04 - SST39SF020A 256kB */
/*	IF (            MREQ & !IORQ & MEM_ADDRESS:[10000..7FFFF] )      OUT FLASH_CS ; /* BANK01, BANK02,... BANK07 (Snapshot storage) - SST39SF040A 512kB */
/*	IF (  PROGRAM & MREQ & !IORQ & MEM_ADDRESS:[80000..8FFFF] )      OUT FLASH_CS ; /* In Programming State, force BANK00 to appear as BANK08 - SST39SF040A 512kB */
	}

CONDITION {
	IF (  PROGRAM & MREQ & !IORQ & MEM_ADDRESS:[00000..0BFFF] )      OUT USB_CS ;   /* PROGRAM state, 48kB of ADDRESS space uses USB fake memory */
	}

/* *************** IO LOGIC *********************/

CONDITION {
	IF ( IORQ & !MREQ & IO_ADDRESS:[2000..3FFF] )                    OUT BREAK ;    /* Initiate the Single Step mode following a BREAK request. */
	IF ( IORQ & !MREQ & IO_ADDRESS:[8000..9FFF] )                    OUT I2C_CS2 ;  /* Selecte the I2C Device #2. */
	IF ( IORQ & !MREQ & IO_ADDRESS:[A000..BFFF] )                    OUT I2C_CS1 ;  /* Selecte the I2C Device #1. */
	}

CONDITION {
	IF ( IORQ & !MREQ & IO_ADDRESS:[4000..5FFF] )                    OUT DIO_CS ;   /* Latch the digital IO, together with !WR or !RD. */
	IF (        !MREQ & IO_ADDRESS:[C000..DFFF] )                    OUT APU_CS ;   /* Selecte the Arithmetic Processing Unit. */
	}